
let {{
// Our algorithm is grouped by rows, but the C one uses columns,
// hence the 'transpose'
toState : [4][32] -> State
toState cols = transpose [ toBytes col | col <- cols ]

// Our state to the C represenation of the state.
fromState : State -> [4][32]
fromState rows = [ fromBytes col | col <- transpose rows ]

withState : (State -> State) -> [4][32] -> [4][32]
withState f x = fromState (f (toState x))


// Key schedule representation

flatSchedule : KeySchedule -> [44][32]
flatSchedule (first,rounds,last) = join [ fromState s | s <- [first] # rounds # [last] ]

unflatShedule : [44][32] -> KeySchedule
unflatShedule (first # rounds # last) =
  ( toState first
  , [ toState r | r <- split rounds ]
  , toState last
  )
}};


emulated_aesenc_substitute_bytes <-
  proof
    proving "emulated_aesenc_substitute_bytes"
    c_code
    "emulated_aesenc_substitute_bytes"
    []
    false
    do { (ptr_data, data) <- allocArrayIn "data" 4 i32;
         crucible_execute_func [ ptr_data ];
         pointsTo ptr_data {{ withState sub_bytes data }};
         returns {{ 1 : [32] }};
       }
    rme;


emulated_aesenc_row_shifting <-
  proof
    proving "emulated_aesenc_row_shifting"
    c_code
    "emulated_aesenc_row_shifting"
    []
    false
    do {
      (ptr_data, data) <- allocArrayIn "data" 4 i32;
      crucible_execute_func [ ptr_data ];
      pointsTo ptr_data {{ withState shift_rows data }};
      returns {{ 1 : [32] }};
    }
    z3;


