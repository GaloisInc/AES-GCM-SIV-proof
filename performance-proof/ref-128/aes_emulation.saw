
let {{
// Our algorithm is grouped by rows, but the C one uses columns,
// hence the 'transpose'
toState : [4][32] -> State
toState cols = transpose [ toBytes col | col <- cols ]

// Our state to the C represenation of the state.
fromState : State -> [4][32]
fromState rows = [ fromBytes col | col <- transpose rows ]

withState : (State -> State) -> [4][32] -> [4][32]
withState f x = fromState (f (toState x))


// Key schedule representation

flatSchedule : KeySchedule -> [44][32]
flatSchedule (first,rounds,last) = join [ fromState s | s <- [first] # rounds # [last] ]

unflatShedule : [44][32] -> KeySchedule
unflatShedule (first # rounds # last) =
  ( toState first
  , [ toState r | r <- split rounds ]
  , toState last
  )
}};


emulated_aesenc_substitute_bytes <-
  proof
    skipping "emulated_aesenc_substitute_bytes"
    c_code
    "emulated_aesenc_substitute_bytes"
    []
    false
    do { (ptr_data, data) <- allocArrayIn "data" 4 i32;
         crucible_execute_func [ ptr_data ];
         pointsTo ptr_data {{ withState sub_bytes128 data }};
         returns {{ 1 : [32] }};
       }
    rme;


emulated_aesenc_row_shifting <-
  proof
    skipping "emulated_aesenc_row_shifting"
    c_code
    "emulated_aesenc_row_shifting"
    []
    false
    do {
      (ptr_data, data) <- allocArrayIn "data" 4 i32;
      crucible_execute_func [ ptr_data ];
      pointsTo ptr_data {{ withState shift_rows128 data }};
      returns {{ 1 : [32] }};
    }
    z3;

emulated_aesenclast <-
  proof
    skipping "emulated_aesenclast"
    c_code
    "emulated_aesenclast"
    [ emulated_aesenc_substitute_bytes
    , emulated_aesenc_row_shifting
    ]
    false
    do {
      (_xmm1, val__xmm1) <- allocArrayIn "_xmm1" 4 i32;
      (_xmm2, val__xmm2) <- allocArrayIn "_xmm2" 4 i32;
      crucible_execute_func [ _xmm1, _xmm2 ];
      let key = {{ toState val__xmm2 }};
      pointsTo _xmm1 {{ withState (aes_final_round128 key) val__xmm1 }};
    }
    z3;


emulated_aesenc <-
  proof
    skipping "emulated_aesenc"
    c_code
    "emulated_aesenc"
    []
    false
    do {
      (_xmm1, val__xmm1) <- allocArrayIn "_xmm1" 4 i32;
      (_xmm2, val__xmm2) <- allocArrayIn "_xmm2" 4 i32;
      crucible_execute_func [ _xmm1, _xmm2 ];
      let key = {{ toState val__xmm2 }};
      pointsTo _xmm1 {{ withState (aes_round128 key) val__xmm1 }};
    }
    z3;



